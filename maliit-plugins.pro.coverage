include(maliit-plugins.pro)

REPORTS_DIR = ../coverage-report
INFO_FILE = maliit-keyboard.info

# Here we override coverage commands to actually create coverage reports.
coverage.commands =
coverage.commands += lcov --directory . --capture --output-file $$INFO_FILE &&
coverage.commands += lcov --directory . --output-file $$INFO_FILE --remove $$INFO_FILE '*/include/Qt*' '*/include/qt4/Qt*' '*/include/maliit/*' '*/include/c++/*' '*presage*.h' '*.moc' '*moc_*.cpp' 'tests/*' &&
coverage.commands += rm -rf $$REPORTS_DIR &&
coverage.commands += $(MKDIR) $$REPORTS_DIR &&
coverage.commands += genhtml --title \"Maliit keyboard\" --output-directory $$REPORTS_DIR $$INFO_FILE
coverage.depends = all check

check.depends += precoverage

QMAKE_EXTRA_TARGETS += precoverage
precoverage.target = precoverage
precoverage.commands = lcov --directory . --zerocounters
